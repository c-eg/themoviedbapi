plugins {
    id "java-library"
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version "1.3.0"
}

repositories {
    mavenCentral()
}

dependencies {
    // logging
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'org.slf4j:jcl-over-slf4j:2.0.12'
    testImplementation 'org.slf4j:slf4j-simple:2.0.12'

    // testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    testImplementation 'org.mockito:mockito-core:5.11.0'

    // util
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'

    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'commons-codec:commons-codec:1.16.1'
    testImplementation 'commons-io:commons-io:2.15.1'
}

// https://stackoverflow.com/questions/34377367/why-is-gradle-install-replacing-my-version-with-unspecified
group 'uk.co.conoregan'
version '1.15.1'

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion '10.12.5'
    configFile file("config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}
tasks.withType(Checkstyle).configureEach {
    reports {
        html.required = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name = "themoviedbapi"
                description = 'A java-wrapper around the JSON API provided by TMdB, which is an open database for movie and film content.'
                url = 'https://github.com/c-eg/themoviedbapi'

                licenses {
                    license {
                        name = 'BSD'
                        url = 'https://github.com/c-eg/themoviedbapi/blob/master/LICENCE.txt'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/c-eg/themoviedbapi.git'
                    url = 'https://github.com/c-eg/themoviedbapi.git'
                }

                developers {
                    developer {
                        id = 'holgerbrandl'
                        name = 'Holger Brandl'
                        email = 'holgerbrandl@gmail.com'
                    }

                    developer {
                        id = 'c-eg'
                        name = 'Conor Egan'
                        email = '17conoregan@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId') && project.hasProperty("signing.password") && project.hasProperty("signing.secretKeyRingFile")) signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

